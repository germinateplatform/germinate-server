import com.bmuschko.gradle.cargo.convention.Deployable
import com.bmuschko.gradle.cargo.tasks.remote.CargoRedeployRemote

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.7.0'
    }
}

plugins {
    id 'java'
    id 'war'
}

apply plugin: 'com.bmuschko.cargo-base'

compileJava.options.encoding = 'UTF-8'

group 'uk.ac.hutton.germinate'
version '4.1.5'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://repo.spring.io/plugins-release/'
    }
    maven {
        url 'http://maven.imagej.net/content/repositories/public/'
    }
}

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])

    implementation 'commons-cli:commons-cli:1.4'
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-fileupload:commons-fileupload:1.4'

    implementation 'org.restlet.jee:org.restlet:2.4.0'
    implementation 'org.restlet.jee:org.restlet.ext.servlet:2.4.0'
    implementation 'org.restlet.jee:org.restlet.ext.fileupload:2.4.0'
    implementation 'org.restlet.jee:org.restlet.ext.json:2.4.0'
    implementation 'org.restlet.jee:org.restlet.ext.gson:2.3.6'
    implementation 'org.json:json:20160212'

    implementation 'mysql:mysql-connector-java:8.0.16'
    implementation 'org.xerial:sqlite-jdbc:3.30.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'cisd:jhdf5:14.12.6'
    implementation 'net.coobird:thumbnailator:0.4.8'
    implementation 'org.jooq:jooq:3.11.10'
    implementation 'org.jooq:jooq-codegen:3.11.10'
    implementation 'org.dhatim:fastexcel-reader:0.10.9'
    implementation 'com.drewnoakes:metadata-extractor:2.14.0'
    implementation 'xerces:xercesImpl:2.12.0'
    implementation 'org.codehaus.castor:castor-core:1.4.1'
    implementation ('org.codehaus.castor:castor-xml:1.4.1') {
        exclude group: 'org.springframework'
    }

    implementation 'org.flywaydb:flyway-core:6.3.3'
    implementation 'com.squareup.okhttp3:okhttp:3.14.6'
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'

    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
}

// Generate a .jar file for the Germinate importer project
task jarForImporter (type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    archiveFileName = 'germinate-core.jar'
    includeEmptyDirs = false
    include '**/germinate/resource/**'
    include '**/server/database/**'
    include '**/Database.class'
    include '**/util/**Utils**'
    include '**/util/**Binding**'
    include '**/util/**Hdf**'
    include '**/auth/**UserType**'
}

// Generate a .jar file that other applications can use to communicate with the API
task jarClient (type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    archiveFileName = 'germinate-client.jar'
    includeEmptyDirs = false
    include '**/germinate/resource/**'
    include '**/pojos/**'
    include '**/StatusMessage**'
    include '**/client/**'
}

// Runs the JOOQ code generation
task codegen (type: JavaExec) {
    group = 'Execution'
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jooq.codegen.GenerationTool'
    args 'jooq.xml'
}

// Generate a .war file
war {
    dependsOn jar, jarForImporter, jarClient

    rootSpec.exclude('**/jhi/**/*.class')
    rootSpec.includeEmptyDirs = false

    // Include external .jar files, but exclude the Germinate client and core.
    classpath fileTree(dir:'build/libs/', include:'*.jar', excludes: ['germinate-client.jar', 'germinate-core.jar'])

    // Set the classpath
    manifest {
        attributes('Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '))
    }

    webInf {
        // Include the .properties file into the classes folder
        from(project.projectDir.toString()) {
            include 'config.properties'
            include 'logging.properties'
            into('classes')
        }
    }

    // Include the client code if it's available
    from("${project.projectDir.toString()}/client") {
        include '**/**.*'
        into('/')
    }
}

// Deploy the created .war file to Tomcat
task deployTomcat (type: CargoRedeployRemote) {
    dependsOn = [war]
    containerId = project.findProperty('tomcat.manager.version') ?: "tomcat8x"
    protocol = project.findProperty('tomcat.manager.protocol') ?: "http"
    hostname = project.findProperty('tomcat.manager.hostname') ?: "localhost"
    port = (project.findProperty('tomcat.manager.port') ?: "8080") as Integer
    username = project.findProperty('tomcat.manager.username') ?: ""
    password = project.findProperty('tomcat.manager.password') ?: ""
    deployables = [new Deployable(files: project.files([war.archiveFile]), context: "${project.'project.name'}/v${project.version}")]
}