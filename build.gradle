import com.bmuschko.gradle.cargo.convention.Deployable
import com.bmuschko.gradle.cargo.tasks.remote.CargoRedeployRemote

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.9.0'
    }
}

plugins {
    id 'java'
    id 'war'
    id 'org.asciidoctor.jvm.convert' version '4.0.2'
}

apply plugin: 'com.bmuschko.cargo-base'

compileJava.options.encoding = 'UTF-8'

group = 'uk.ac.hutton.germinate'
version = '4.9.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven {
        url = 'https://repo.osgeo.org/repository/release/'
    }
    mavenCentral()
    maven {
        url = 'https://maven.scijava.org/content/repositories/public/'
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.glassfish.jersey.connectors:jersey-apache5-connector:3.1.3'
    testRuntimeOnly 'org.slf4j:slf4j-nop:2.0.17'

    implementation fileTree(dir: 'lib', include: ['*.jar'])

    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'commons-io:commons-io:2.19.0'

    implementation 'com.drewnoakes:metadata-extractor:2.18.0'

    implementation 'org.json:json:20250517'
    implementation 'com.google.code.gson:gson:2.10'

    implementation 'com.mysql:mysql-connector-j:8.2.0'
    implementation 'org.xerial:sqlite-jdbc:3.44.0.0'
    implementation 'cisd:jhdf5:19.04.1'
    implementation 'net.coobird:thumbnailator:0.4.18'
//    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.jooq:jooq:3.20.5'
    implementation 'org.dhatim:fastexcel-reader:0.19.0'
    implementation 'xerces:xercesImpl:2.12.2'
    implementation 'org.codehaus.castor:castor-core:1.4.1'
    implementation ('org.codehaus.castor:castor-xml:1.4.1') {
        exclude group: 'org.springframework'
    }

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.2'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

    implementation 'org.flywaydb:flyway-core:10.22.0'
    implementation 'org.flywaydb:flyway-mysql:10.22.0'
    implementation 'com.squareup.retrofit2:retrofit:2.12.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'de.poiu.apron:apron:2.1.1'

    implementation 'de.ipk-gatersleben:isa4j:1.1.0'

    providedCompile 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:3.1.3'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.3'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.1.3'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.1.3'

    implementation 'javax.media:jai_core:1.1.3'
    implementation 'org.geotools:gt-shapefile:27.2'

    implementation 'net.logicsquad:nanocaptcha:1.3'

    implementation 'info.picocli:picocli:4.7.7'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'

    // Disabled Swagger until we can figure out how to make it work with Jersey3 and Jakarta
    implementation 'io.swagger.core.v3:swagger-jaxrs2-jakarta:2.2.31'
    implementation 'io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-jakarta:2.2.31'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn(classes)
    group = "Documentation"
    description = "Generate AsciiDoc manpage"
    classpath(configurations.compileClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    mainClass = 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args "jhi.germinate.GerminateCommand", "--outdir=${project.layout.buildDirectory.get()}/generated-picocli-docs", "-v", "-f"
}

apply plugin: 'org.asciidoctor.jvm.convert'
asciidoctor {
    dependsOn(generateManpageAsciiDoc)
    sourceDir = file("${project.layout.buildDirectory.get()}/generated-picocli-docs")
    outputDir = file("${project.projectDir.toString()}/docs/cli")
    logDocuments = true
    outputOptions {
        backends = ['html5']
    }
}

// Generate a .jar file for the Germinate importer project
task jarForImporter (type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    archiveFileName = "germinate-core-${project.version}.jar"
    includeEmptyDirs = false
    include '**/germinate/resource/**'
    include '**/Database.class'
    include '**/util/**Utils**'
    include '**/util/**/**hdf**/**'
    include '**/auth/**UserType**'
    include '**/Secured**'
    include '**/PermitAll**'
    include '**/NeedsDatasets**'
    include '**/AuthenticationFilter**'
    include '**/AuthorizationFilter**'
    include '**/**DatasetTableResource**'
    include '**/DataOrientation**'
    include '**/ExportResource**'
    include '**/BaseResource**'
    include '**/ContextResource**'
    include '**/IFilteredResource**'
    include '**/PropertyWatcher**'
    include '**/GerminateCommandVersion**'
}

// Generate a .jar file that other applications can use to communicate with the API
task jarClient (type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    archiveFileName = "germinate-client-${project.version}.jar"
    includeEmptyDirs = false
    include '**/germinate/resource/**'
    include '**/pojos/**'
    include '**/StatusMessage**'
    include '**/client/**'
}

jar {
    // Set the classpath
    manifest {
        attributes('Implementation-Version': version)
        attributes('Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '))
        attributes('Main-Class': 'jhi.germinate.GerminateCommand')
    }
}

// Generate a .war file
war {
    dependsOn jar, jarForImporter, jarClient

    rootSpec.exclude('**/jhi/**/*.class')
    rootSpec.includeEmptyDirs = false

    // Include external .jar files, but exclude the Germinate client and core.
    classpath fileTree(dir:'build/libs/', include:'*.jar', excludes: ['germinate-client-*.jar', 'germinate-core-*.jar'])

    // Set the classpath
    manifest {
        attributes('Implementation-Version': version)
        attributes('Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '))
    }

    webInf {
        // Include the .properties file into the classes folder
        from(project.projectDir.toString()) {
            include 'config.properties'
            include 'logging.properties'
            into('classes')
        }
    }

    // Include the client code if it's available
    from("${project.projectDir.toString()}/client") {
        include '**/**.*'
        into('/')
    }
}

// Deploy the created .war file to Tomcat
task deployTomcat (type: CargoRedeployRemote) {
    dependsOn = [war]
    containerId = project.findProperty('tomcat.manager.version') ?: "tomcat10x"
    protocol = project.findProperty('tomcat.manager.protocol') ?: "http"
    hostname = project.findProperty('tomcat.manager.hostname') ?: "localhost"
    port = (project.findProperty('tomcat.manager.port') ?: "8080") as Integer
    username = project.findProperty('tomcat.manager.username') ?: ""
    password = project.findProperty('tomcat.manager.password') ?: ""
    deployables = [new Deployable(files: project.files([war.archiveFile]), context: "${project.'project.name'}/v${project.version}")]
}

test {
    useJUnitPlatform()
}

// Automatically build both jars on every build
rootProject.tasks.getByName('build').dependsOn jarClient
rootProject.tasks.getByName('build').dependsOn jarForImporter